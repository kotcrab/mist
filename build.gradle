buildscript {
  ext.kotlinVersion = '1.9.24'
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  }
}

subprojects {
  group 'com.kotcrab.mist'
  version = '0.0.1'
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'kotlin'

  ext {
    gdxVersion = '1.9.9'
    visUiVersion = '1.4.2'
    ktxVersion = '1.9.9-b1'
    jacksonVersion = '2.17.1'
  }

  repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
  }

  kotlin {
    jvmToolchain(11)
  }
}

project(':core') {
  dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    api "com.github.kotcrab:kio:9111dcc"
    api "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    api "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    api "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
  }
}

project(':asm') {
  dependencies {
    api project(":core")
    testImplementation "com.kotcrab.kmips:kmips:1.2"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.1"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.3.1"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.3.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.3.1"
    testImplementation "org.assertj:assertj-core:3.11.1"
    testImplementation "io.mockk:mockk:1.8.7"
    testImplementation "net.bytebuddy:byte-buddy:1.8.22"
    testImplementation "net.bytebuddy:byte-buddy-agent:1.8.22"
  }
}

project(':decompiler') {
  dependencies {
    api project(":core")
    api project(":asm")
    implementation "org.apache.commons:commons-collections4:4.0"
  }
}

project(':ghidra-client') {
  dependencies {
    api project(":core")
    implementation "io.github.rybalkinsd:kohttp:0.12.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.1"
  }
}

project(':symbolic') {
  dependencies {
    api project(":core")
    api project(":asm")
    api project(":ghidra-client")
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.1"
    implementation "io.ksmt:ksmt-core:0.5.23"
    implementation "io.ksmt:ksmt-bitwuzla:0.5.23"
    implementation "com.kotcrab.kmips:kmips:1.7"
    implementation "com.github.kotcrab:kmipsx:87feb7a"
  }
}

project(':ui-gdx') {
  apply plugin: 'application'
  mainClassName = "mist.AppKt"

  dependencies {
    api project(":core")
    api project(":asm")
    api project(":decompiler")
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.1"
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation "com.kotcrab.vis:vis-ui:$visUiVersion"
    implementation "io.github.libktx:ktx-vis:$ktxVersion"
    implementation "io.github.libktx:ktx-actors:$ktxVersion"
    implementation "io.github.libktx:ktx-async:$ktxVersion"
    implementation "io.github.libktx:ktx-assets:$ktxVersion"
    implementation "io.github.libktx:ktx-inject:$ktxVersion"
  }
}
